#@TYPE: Machine
#@NAME: x86-chromebook

#@DESCRIPTION: Machine configuration for 64 bit Intel Core i7 CPU (Generic at this point, based on meta-intel's TUNE)

# Keep the same structure as meta-intel
require conf/machine/include/meta-intel.inc
require conf/machine/include/intel-corei7-64-common.inc
require conf/machine/include/intel-common-pkgarch.inc


MACHINE_FEATURES += "pcbios efi"
MACHINE_FEATURES += "wifi 3g nfc"
MACHINE_FEATURES += "intel-ucode"

MACHINE_HWCODECS ?= "intel-vaapi-driver gstreamer1.0-vaapi"

# Enable optional dpdk:
COMPATIBLE_MACHINE_pn-dpdk = "x86-chromebook"

# Enable optional QAT items:
COMPATIBLE_MACHINE_pn-qat16 = "x86-chromebook"
COMPATIBLE_MACHINE_pn-qat17 = "x86-chromebook"
COMPATIBLE_MACHINE_pn-zlib-qat = "x86-chromebook"

XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_I915} \
            ${XSERVER_X86_I965} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_VESA} \
            ${XSERVER_X86_ASPEED_AST} \
           "

SYSLINUX_OPTS = "serial 0 115200"
SERIAL_CONSOLES = "115200;ttyS0 115200;ttyS1 115200;ttyS2"
APPEND += "rootwait console=ttyS0,115200 console=tty0"

IMAGE_FSTYPES += "wic"
# WKS file that supports the legacy boot mode along with EFI
WKS_FILE="systemd-bootdisk-microcode-legacy.wks.in"
# Support only systemd-boot at this point, the chromebooks only allow you to boot in Legacy boot (BIOS) mode anyway.
# WKS_FILE ?= "${@bb.utils.contains_any("EFI_PROVIDER", "systemd-boot", "systemd-bootdisk-microcode.wks.in", "grub-bootdisk-microcode.wks.in", d)}"
WKS_FILE_DEPENDS_append = " intel-microcode"


# Since this MACHINE should work on most x86 chromebooks, we have to rely on kernel-modules being installed (these are set as extra on meta-intel, we put them on ESSENTIAL instead).
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_append = " kernel-modules linux-firmware"


# Needed for QEMU, although at this point -cpu Skylake-{Slient,Server} has some issues
IMAGE_CLASSES += "qemuboot"

QB_SYSTEM_NAME_x86-chromebook = "qemu-system-x86_64"
QB_CPU_x86-chromebook = "-cpu Nehalem"
QB_CPU_KVM_x86-chromebook = "-cpu kvm64"

QB_AUDIO_DRV = "alsa"
QB_AUDIO_OPT = "-soundhw ac97,es1370"
QB_KERNEL_CMDLINE_APPEND = "vga=0 uvesafb.mode_option=640x480-32 oprofile.timer=1 uvesafb.task_timeout=-1"
# Add the 'virtio-rng-pci' device otherwise the guest may run out of entropy
QB_OPT_APPEND = "-vga vmware -show-cursor -usb -usbdevice tablet -device virtio-rng-pci"

# Not a huge fan of setting this on a MACHINE.conf, but apparently this is the
# way its wired in, setting it on an image recipe wouldn't work, and otherwise
# it would have to be set on several bbappends.
# This allows us to run Xorg as a non root user
ROOTLESS_X = "1"



# Support linux-intel and linux-yocto, but since this is generic keep linux-intel as default.
PREFERRED_PROVIDER_virtual/kernel ?= "linux-intel"
#PREFERRED_PROVIDER_virtual/kernel = "linux-yocto"

# Branch used for chromium tools, e.g. vboot, flashrom
CHROMIUM_THIRDPARTY_BRANCH = "release-R80-12739.B"

# Needed to build MrChromebox tools
CHROMIUM_CPU_VARIANT ?= "kbl"