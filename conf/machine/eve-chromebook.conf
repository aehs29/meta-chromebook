#@TYPE: Machine
#@NAME: eve-chromebook

#@DESCRIPTION: Machine configuration for 64 bit Intel Core i7 CPU (at this point supporting Kaby Lake / Skylake) with all their possible extensions.

# This file is based on meta-intel intel-corei7 MACHINE, but it provides compatibility with newer
# generations of the architecture (they are still at Nehalem).

# The Skylake tune uses linux-intel as default, comment the following to use it if desired
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"
PREFERRED_VERSION_linux-yocto  = "5.%"


# Keep the same structure as meta-intel
require conf/machine/include/meta-intel.inc
require conf/machine/include/x86-base.inc
require conf/machine/include/tune-skylake.inc
require conf/machine/include/intel-common-pkgarch.inc

MACHINE_FEATURES += "efi"
MACHINE_FEATURES += "wifi 3g nfc"
MACHINE_FEATURES += "intel-ucode"

MACHINE_FEATURES += "pcbios"
MACHINE_FEATURES += "alsa"

MACHINE_HWCODECS ?= "intel-vaapi-driver gstreamer1.0-vaapi"


XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_I915} \
            ${XSERVER_X86_I965} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_VESA} \
            ${XSERVER_X86_ASPEED_AST} \
           "

SYSLINUX_OPTS = "serial 0 115200"
SERIAL_CONSOLES = "115200;ttyS0 115200;ttyS1 115200;ttyS2"
APPEND += "rootwait console=ttyS0,115200 console=tty0"


IMAGE_FSTYPES += "wic"
# Support only systemd-boot at this point, the chromebooks only allow you to boot in Legacy boot (BIOS) mode anyway.
# WKS_FILE ?= "${@bb.utils.contains_any("EFI_PROVIDER", "systemd-boot", "systemd-bootdisk-microcode.wks.in", "grub-bootdisk-microcode.wks.in", d)}"
# WKS file that supports the legacy boot mode along with EFI
WKS_FILE="systemd-bootdisk-microcode-legacy.wks.in"
WKS_FILE_DEPENDS_append = " intel-microcode"

# Specify the required kernel modules (and firmware) for the specific HW on this MACHINE
# Drivers/Firmware
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS_append = " kernel-module-i915 linux-firmware-i915 kernel-module-iwlwifi linux-firmware-iwlwifi-7265d kernel-module-iwlmvm linux-firmware-ibt-hw-37-8 kernel-module-cfg80211 kernel-module-mac80211"


# Needed for QEMU, although at this point -cpu Skylake-{Slient,Server} has some issues
IMAGE_CLASSES += "qemuboot"

QB_SYSTEM_NAME_eve-chromebook = "qemu-system-x86_64"
QB_CPU_eve-chromebook = "-cpu Skylake-Client"
QB_CPU_KVM_eve-chromebook = "-cpu kvm64"

QB_AUDIO_DRV = "alsa"
QB_AUDIO_OPT = "-soundhw ac97,es1370"
QB_KERNEL_CMDLINE_APPEND = "vga=0 uvesafb.mode_option=640x480-32 oprofile.timer=1 uvesafb.task_timeout=-1"
# Add the 'virtio-rng-pci' device otherwise the guest may run out of entropy
QB_OPT_APPEND = "-vga vmware -show-cursor -usb -usbdevice tablet -device virtio-rng-pci"



# Not a huge fan of setting this on a MACHINE.conf, but apparently this is the
# way its wired in, setting it on an image recipe wouldn't work, and otherwise
# it would have to be set on several bbappends.
# This allows us to run Xorg as a non root user
ROOTLESS_X = "1"

# qemu-usermode is removed from MACHINE_FEATURES on the tune-skylake.inc file,
# we need to add it back to be able to build all components properly
INHERIT += "qemuuser-enable-chromebook"

# Branch used for chromium tools, e.g. vboot, flashrom
CHROMIUM_THIRDPARTY_BRANCH = "release-R80-12739.B"

# Needed to build MrChromebox tools
CHROMIUM_CPU_VARIANT = "kbl"
